plugins {
    id 'java'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
group 'com.dbclient.jdbc'
version '1.0'

tasks.jar {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    manifest.attributes["Main-Class"] = "com.dbclient.jdbc.server.JdbcExecutorServer"
}

task fatJar(type: Jar) {
    group = 'build'
    manifest.from jar.manifest
    classifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    destinationDir = file("$rootDir/vscode-dbclient-jdbc")
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/**"
    }
    with jar
}

repositories {
    mavenCentral()
}

// https://mvnrepository.com
dependencies {
    // Logging
    implementation 'ch.qos.logback:logback-core:1.2.6'
    implementation 'ch.qos.logback:logback-classic:1.2.6'

    // Jackson
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.2'

    // Azure
    implementation('com.microsoft.azure:msal4j:1.19.1') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    // Oracle
    implementation 'xerces:xercesImpl:2.12.2'
    implementation 'com.oracle.database.xml:xdb6:18.3.0.0'
    implementation 'com.oracle.database.jdbc:ojdbc8:21.5.0.0'

    // Compress
    implementation 'org.apache.commons:commons-compress:1.27.1'

    // Lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
}

test {
    useJUnitPlatform()
}
